apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'io.github.gradle-nexus.publish-plugin'

def buildVersion = "2.0.0-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"
group = "uk.gov.ida"
version = "$buildVersion"
archivesBaseName = "verify-gradle"

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Verify Gradle'
                packaging = 'jar'
                description = 'Library for various SAML testing utility operations.'
                url = 'https://github.com/alphagov/verify-gradle'
                artifactId = 'verify-gradle'

                scm {
                    url = 'https://github.com/alphagov/verify-gradle'
                    connection = 'scm:git:git://github.com/alphagov/verify-gradle.git'
                    developerConnection = 'scm:git:ssh://git@github.com:alphagov/verify-gradle.git'
                }

                licenses {
                    license {
                        name = 'MIT Licence'
                        url = 'https://github.com/alphagov/verify-gradle/blob/master/LICENCE'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        name = 'GDS Developers'
                    }
                }
            } 
        }
    }
}

nexusPublishing {
    useStaging = true
    repositories {
        sonatype {
            // because we registered in Sonatype after 24 Feb 2021, we provide these URIs
            // see: https://github.com/gradle-nexus/publish-plugin/blob/master/README.md
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = System.getenv("SONATYPE_USERNAME")
            password = System.getenv("SONATYPE_PASSWORD")
        }
    }
}

signing {
    useInMemoryPgpKeys(
        System.getenv("MAVEN_CENTRAL_SIGNING_KEY"),
        System.getenv("MAVEN_CENTRAL_SIGNING_KEY_PASSWORD")
    )
    sign publishing.publications
}
